buildscript {
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.1.0'
    }
}

plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.namtium-oxide:namtium:fa3b3601c7'
    implementation 'org.slf4j:slf4j-simple:1.7.33'
}

application {
    mainClass = 'com.lion328.namtium.example.Main'
}

project.version = '1.0';

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
                    "$buildDir/generated/java",
            ]
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    template {
        java {
            srcDirs = ['src/template/java']
        }
    }
}

task generateSources(type: Copy) {
    def vars = [
            'version': project.version,
            'name': project.name,
    ]
    inputs.properties vars
    from('src/template/java')
    into "$buildDir/generated/java"
    expand vars
}

compileJava.dependsOn generateSources

task proguard(type: proguard.gradle.ProGuardTask) {
    configuration 'proguard.cfg'

    dependsOn shadowJar
    injars shadowJar
    outjars "$buildDir/libs/${project.name}-${project.version}-obfuscated.jar"
    printmapping "$buildDir/proguard.map"

    if (System.getProperty('java.version').startsWith('1.')) {
        libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    } else {
        new File("${System.getProperty('java.home')}/jmods").listFiles().each {
            libraryjars it, jarfilter: '!**.jar', filter: '!module-info.class'
        }
    }
}

assemble.dependsOn proguard
